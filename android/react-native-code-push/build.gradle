apply plugin: "com.android.library"

def DEFAULT_COMPILE_SDK_VERSION             = 26
def DEFAULT_BUILD_TOOLS_VERSION             = "26.0.3"
def DEFAULT_TARGET_SDK_VERSION              = 26
def DEFAULT_MIN_SDK_VERSION                 = 16
def DEFAULT_REACTNATIVE_VERSION             = "0.56.0"

ext {
    reactnativeVersion = project.hasProperty('reactNativeVersion') ? project.reactNativeVersion : DEFAULT_REACTNATIVE_VERSION
}


android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion rootProject.hasProperty('minSdkVersion') ? rootProject.minSdkVersion : DEFAULT_MIN_SDK_VERSION
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    implementation "com.facebook.react:react-native:${reactnativeVersion}"
    implementation 'com.nimbusds:nimbus-jose-jwt:5.1'
}

ext {
    PUBLISH_GROUP_ID = 'com.chengkai.helper'
    PUBLISH_ARTIFACT_ID = 'react-native-code-push'
    PUBLISH_VERSION = android.defaultConfig.versionName
}
apply plugin: 'maven'
uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId project.PUBLISH_GROUP_ID
            artifactId project.PUBLISH_ARTIFACT_ID
            version project.PUBLISH_VERSION
        }
    }
}